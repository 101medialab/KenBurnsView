buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}

repositories {
    mavenCentral()
    jcenter()
}

group = 'com.github.goka.kenburnsview'
version = '1.0.3'

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion project.ANDROID_BUILD_SDK_VERSION as int
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion project.ANDROID_BUILD_TARGET_SDK_VERSION as int
        versionCode project.VERSION_CODE as int
        versionName project.VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:27.0.2'

    compile 'com.github.bumptech.glide:glide:4.4.0'
    compile "com.github.bumptech.glide:okhttp3-integration:4.4.0"
    compile ("com.github.bumptech.glide:recyclerview-integration:4.4.0") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    annotationProcessor 'com.github.bumptech.glide:compiler:4.4.0'
}

def siteUrl = 'https://github.com/gotokatsuya/KenBurnsView'
def gitUrl = 'https://github.com/gotokatsuya/KenBurnsView.git'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.key')

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'KenBurnsView'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/gotokatsuya/KenBurnsView/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['Android']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'KenBurnsView'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id properties.getProperty('bintray.id')
                        name properties.getProperty('bintray.name')
                        email properties.getProperty('bintray.email')
                    }
                }
                scm {
                    connection 'https://github.com/gotokatsuya/KenBurnsView.git'
                    developerConnection 'https://github.com/gotokatsuya/KenBurnsView.git'
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions {
    doLast {
        println project.getConvention()
    }
}